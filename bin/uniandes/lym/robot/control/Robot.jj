/*
Gramatica para el Robot:
Autor: Alejandro Arango
Modificado por: Silvia Takahashi
Modificado por: Juan Pablo Morales.
Agregadas producciones para manejar globos
Modificado por Silvia Takahashi
**/


options 
{
  
	LOOKAHEAD=1; 
	IGNORE_CASE=true;
	STATIC = false;
	
}
   

PARSER_BEGIN(Robot)

package uniandes.lym.robot.control;

import uniandes.lym.robot.kernel.*;
import uniandes.lym.robot.view.Console;

import java.util.HashMap;
import java.util.Map;
import java.util.List;
import java.util.ArrayList;
import java.awt.Point;
import java.io.*;
import java.util.Vector;
import java.util.LinkedList;

@SuppressWarnings("serial")
public class Robot 
{
	public static Map<String, Integer> variableMap = new HashMap<>();
	public static Map<String, List<String>> functionMap = new HashMap<>();
	public static Map<String, String> paramFunctionMap = new HashMap<>();
	public static String salida = new String();
	
	private RobotWorldDec world;
	
	
	void setWorld(RobotWorld w) {
		world = (RobotWorldDec) w;	
	}

	void runDirsMethod(List<String> list) {
	  int initialOrientation = 0;
	  String direction;
	  for(int i = 1; i < list.size(); i++) {
	    initialOrientation = world.getFacing();
        direction = list.get(i);
        if (direction.equals(":right"))
        {
          world.turnRight();
          world.moveForward(1, false);
        }
        else if (direction.equals(":left"))
        {
          world.turnRight();
          world.turnRight();
          world.turnRight();
          world.moveForward(1, false);
        }
        else if (direction.equals(":front"))
        {
          world.moveForward(1, false);
        }
        else if (direction.equals(":back"))
        {
          world.turnRight();
          world.turnRight();
          world.moveForward(1, false);
        }
		//Volver a orientacion de inicio
        while (world.getFacing() != initialOrientation)
        {
          world.turnRight();
        }

	    }
	  }
	  public void assignMethod(String x, String y) throws Error, ParseException
	{
	  String variableName = x;
	  int variableValue = -1;
	  try
	  {
	    variableValue = Integer.parseInt(y);
	  }
	  catch (NumberFormatException ee)
	  {
	    throw new Error("Hay overflow con el valor: "+ y);
	  }
	  
	  if (!Robot.variableMap.containsKey(variableName))
	  {
	    throw new ParseException("Error: La variable " + variableName + " no está definida.");
	  }
	  if (variableValue != -1)
	  {
	    Robot.variableMap.put(variableName, variableValue);
	    Robot.salida = "Command: variable assignment";
	    }
	}

	public void moveMethod(String x) throws Error
	{
	  int numberSteps = -1;
	  try
	  {
	    numberSteps = Integer.parseInt(x);
	  }
	  catch (NumberFormatException ee)
	  {
	    throw new Error("Hay overflow con el valor: "+x);
	  }
	  if (numberSteps != -1)
	  {
	    world.moveForward(numberSteps, false);
	    Robot.salida = "Command: move";
	  }
	}

	public void skipMethod(String x) throws Error
	{
	  int numberValue = -1;
	  try
	  {
	    numberValue = Integer.parseInt(x);
	  }
	  catch (NumberFormatException ee)
	  {
	    throw new Error("Hay overflow con el valor: "+x);
	  }
	  if (numberValue != -1)
	  {
	    world.moveForward(numberValue, true);
	    Robot.salida = "Command: skip";
	  }
	}

	public void turnMethod(String x) throws ParseException
	{
	  String direction = x;
      switch(direction) {
	    case ":left":
	        world.turnRight();
	        world.turnRight();
	        world.turnRight();
	        break;
	    case ":right":
	        world.turnRight();
	        break;
	    case ":around":
	        world.turnRight();
	        world.turnRight();
	        break;
	    default:
	        throw new ParseException("Error: la dirección " + direction + " no es válida con el comando turn.");
		}
      Robot.salida = "Command: turn";
	}

	public void faceMethod(String x)
	{
	  String orientationTokenValue = x;
	  int orientationValue = -1;
      if(orientationTokenValue.equals(":north"))
      {
        orientationValue = 0;
      }
      else if (orientationTokenValue.equals(":south"))
      {
        orientationValue = 1;
      }
      else if (orientationTokenValue.equals(":east"))
      {
        orientationValue = 2;
      }
      else if (orientationTokenValue.equals(":west"))
      {
        orientationValue = 3;
      }
      while (world.getFacing() != orientationValue)
      {
        world.turnRight();
      }
      Robot.salida = "Command: face";
	}

	public void putMethod(String x, String y) throws Error
	{
	  String item = x;
	  int numberItem = -1;
	  try
	  {
	    numberItem = Integer.parseInt(y);
	  }
	  catch (NumberFormatException ee)
	  {
	    throw new Error("Hay overflow con el valor: "+y);
	  }
	  if (numberItem != -1)
	  {
	    if (item.equals(":balloons"))
	    {
	      world.putBalloons(numberItem);
	    }
	    else if (item.equals(":chips"))
	    {
	      world.putChips(numberItem);
	    }
	    Robot.salida = "Command: put";
	  }
	}

	public void pickMethod(String x, String y) throws Error
	{
	  String item = x;
	  int numberItem = -1;
	  try
	  {
	    numberItem = Integer.parseInt(y);
	  }
	  catch (NumberFormatException ee)
	  {
	    throw new Error("Hay overflow con el valor: "+y);
	  }
	  if (numberItem != -1)
	  {
	    if (item.equals(":balloons"))
	    {
	      world.grabBalloons(numberItem);
	    }
	    else if (item.equals(":chips"))
	    {
	      world.pickChips(numberItem);
	    }
	    Robot.salida = "Command: pick";
	  }
	}

	public void moveDirMethod(String x, String y) throws Error
	{
	  String item = y;
	  int numberItem = -1;
	  try
	  {
	    numberItem = Integer.parseInt(x);
	  }
	  catch (NumberFormatException ee)
	  {
	    throw new Error("Hay overflow con el valor: "+x);
	  }
	  if (numberItem != -1)
	  {
		//guardar orientacion inicial
	    int initialOrientation = world.getFacing();
	    if (item.equals(":right"))
	    {
	      world.turnRight();
	      world.moveForward(numberItem, false);
	    }
	    else if (item.equals(":left"))
	    {
	      world.turnRight();
	      world.turnRight();
	      world.turnRight();
	      world.moveForward(numberItem, false);
	    }
	    else if (item.equals(":front"))
	    {
	      world.moveForward(numberItem, false);
	    }
	    else if (item.equals(":back"))
	    {
	      world.turnRight();
	      world.turnRight();
	      world.moveForward(numberItem, false);
	    }
	    while (world.getFacing() != initialOrientation)
	    {
		  //Volver a orientacion de inicio
	      world.turnRight();
	    }
	    Robot.salida = "Command: move-dir";
	  }
	}

	public void moveFaceMethod(String x, String y) throws Error
	{
	  String orientation = y;
	  int moveValue = -1;
	  int orientationValue = -1;
	  try
	  {
	    moveValue = Integer.parseInt(x);
	  }
	  catch (NumberFormatException ee)
	  {
	    throw new Error("Hay overflow con el valor: "+x);
	  }
	  if (moveValue != -1)
	  {
	    if(orientation.equals(":north"))
	    {
	      orientationValue = 0;
	    }
	    else if (orientation.equals(":south"))
	    {
	      orientationValue = 1;
	    }
	    else if (orientation.equals(":east"))
	    {
	      orientationValue = 2;
	    }
	    else if (orientation.equals(":west"))
	    {
	      orientationValue = 3;
	    }
	    while (world.getFacing() != orientationValue)
	    {
	      world.turnRight();
	    }
	    world.moveForward(moveValue, false);
	    Robot.salida = "Command: move-face";
	  }
	}
}
PARSER_END(Robot)

SKIP:
{
	" "
	|	"\r"
	|	"\t"
	| "\n"
}

TOKEN: /*Commands*/
{
	 <ASSIGN: "=">
	| <MOVE:  "move">
	| <NAME: "name"> 
	| <ROBOTSKIP: "skip">
	| <TURN: "turn">
	| <FACE: "face">
	| <PUT: "put" >
	| <PICK: "pick" >
	| <MOVEDIR: "move-dir" >
	| <RUNDIRS: "run-dirs">
	| <MOVEFACE: "move-face">
	| <NULL: "null">		

}

TOKEN: /*Constants*/
{
	<ITEM: ":balloons" | ":chips" >
	| <DIRECTION: ":left" | ":right" | ":around" | ":front" | ":back">
	| <ORIENTATION: ":north" | ":south" | ":west" | ":east">
	| <CONSTANT: "dim" | "myxpos" | "myypos" | "mychips" | "myballons" | "ballonshere" | "chipshere" | "spaces" >

}

TOKEN: /*Conditions and Conditionals*/
{
	<IF: "if">
	| <LOOP: "loop">
	| <REPEAT: "repeat"> 
	| <FACING: "facing?">
	| <BLOCKED: "blocked?">
	| <CANPUT: "can-put?">
	| <CANPICK: "can-pick?">
	| <CANMOVE: "can-move?">
	| <ISZERO: "isZero?">
	| <NOT: "not">
}

TOKEN : /*Additional Tokens*/
{
	<NUM: (<DIGIT>)+ > 
	| <LPAREN: "(">
	| <RPAREN: ")">
	| <DEFVAR: "defvar">
	| <DEFUN: "defun">
	| <WORD: (["a"-"z", "0"-"9"])+ >
	| <#DIGIT: ["0"-"9"] >   
}

/*Variable and Procedure Definitions*/
void varDefinition() :
{
  Token variableName;
  int variableValue;
  
}
{
  <DEFVAR>(variableName = <WORD>)variableValue = value()<RPAREN>
  {Robot.variableMap.put(variableName.image, variableValue);
	Robot.salida = "Command: defvar";
  }
}

void procedureDefinition() :
{
  Token tokenFunName;
  List<String> paramsList = new ArrayList<>();
}
{
  <DEFUN>(tokenFunName = <WORD>){
	Robot.functionMap.put(tokenFunName.image, paramsList);
    }(<LPAREN>(params(tokenFunName.image))<RPAREN>)(<LPAREN>instruction(false))+<RPAREN>
    {
      Robot.paramFunctionMap.clear();
      Robot.salida = "Command: procedureDefinition";
    }
}

void params(String funName) :
{
  Token tokenParamName;
}
{
  (tokenParamName = <WORD> {
	Robot.functionMap.get(funName).add(tokenParamName.image);
	Robot.paramFunctionMap.put(tokenParamName.image, funName);
    })*
}

/*Instructions*/
void instruction(boolean runWorld) :
{}
{
  //TODO: Revisar si procedureCall necesita runWorld (creería que si porque dentro se pueden tener estructuras de control, en particular if's)
  (procedureCall()|parseCommand(runWorld, null)|controlStructure(runWorld))<RPAREN>
}

void block(boolean runWorld) :
{}
{
  <LPAREN>((<LPAREN>instruction(runWorld))+<RPAREN>|instruction(runWorld))
}

//TODO: Modificar para que en efecto corra la función
void procedureCall() :
{
  Token funName;
  List<String> paramTokens = new ArrayList<>();
}
{
  {
    Robot.salida = "Command: procedureCall";
  }
  (funName = <WORD>)(<NUM>{paramTokens.add(token.image);}|
  <CONSTANT>{paramTokens.add(token.image);}|
  varCall(){paramTokens.add(token.image);})*
  {
    if (paramTokens.size() != Robot.functionMap.get(funName.image).size())
    {
      throw new ParseException("Error: La cantidad de parámetros ingresados no coincide con la cantidad esperada.");
    }
  }
}

void parseCommand(boolean runWorld, List<String> list) :
{}
{
  (assign(runWorld, list)
  |move(runWorld, list)
  |skip(runWorld, list)
  |turn(runWorld, list)
  |face(runWorld, list)
  |put(runWorld, list)
  |pick(runWorld, list)
  |moveDir(runWorld, list)
  |runDirs(runWorld, list)
  |moveFace(runWorld, list)
  |nullCommand()
  )
  
}

/*Commands*/
void assign(boolean runWorld, List<String> list):
{
  String variableName;
  int variableValue;
  Token assign;
  Token word;
}
{
  assign = < ASSIGN > word =< WORD >
  //No se llama varCall() porque permitiría que se hiciera assign sobre el parámetro de una función
  {
    variableName = token.image;
    if (!Robot.variableMap.containsKey(variableName))
    {
      throw new ParseException("Error: La variable " + variableName + " no está definida.");
    }
  }
  variableValue = value()
  {
    if (variableValue != -1 && runWorld)
    {
      Robot.variableMap.put(variableName, variableValue);
      Robot.salida = "Command: variable assignment";
    }

    if (list != null) {
      list.add(assign.image);
      list.add(word.image);
      list.add(Integer.toString(variableValue));
       }
  }
}

void move(boolean runWorld, List<String> list):
{int numberSteps;
Token moveToken;
}
{
  moveToken = < MOVE >numberSteps=value()
  {
    if (numberSteps != -1 && runWorld)
    {
      world.moveForward(numberSteps, false);
      Robot.salida = "Command: move";
    }
    if (list != null) {
    	list.add(moveToken.image);
    	list.add(Integer.toString(numberSteps));
       }
  }
}

void skip(boolean runWorld, List<String> list):
{
  int numberValue;
  Token robotSkip;

}
{
 robotSkip = < ROBOTSKIP > numberValue = value()
  {
    if (numberValue != -1 && runWorld)
    {
      world.moveForward(numberValue, true);
      Robot.salida = "Command: skip";
    }

    if (list != null) {
      list.add(robotSkip.image);
      list.add(Integer.toString(numberValue));
      }
  }
}

void turn(boolean runWorld, List<String> list):
{String direction;
Token turn;
Token directionToken;
}
{
  turn = < TURN > directionToken =< DIRECTION >
  {
    if (runWorld)
    {
      direction = directionToken.image;
      switch(direction) {
	    case ":left":
	        world.turnRight();
	        world.turnRight();
	        world.turnRight();
	        break;
	    case ":right":
	        world.turnRight();
	        break;
	    case ":around":
	        world.turnRight();
	        world.turnRight();
	        break;
	    default:
	        throw new ParseException("Error: la dirección " + token.image + " no es válida con el comando turn.");
		}
      Robot.salida = "Command: turn";
    }

    if (list != null) {
      list.add(turn.image);
      list.add(directionToken.image);
      }
  }
}

void face(boolean runWorld, List<String> list):
{
  String orientationTokenValue;
  int orientationValue = -1;
  Token face;
  Token orientation;
}
{
 face = < FACE > orientation = < ORIENTATION >
  {
    if (runWorld)
    {
      orientationTokenValue = orientation.image;
      if(orientationTokenValue.equals(":north"))
      {
        orientationValue = 0;
      }
      else if (orientationTokenValue.equals(":south"))
      {
        orientationValue = 1;
      }
      else if (orientationTokenValue.equals(":east"))
      {
        orientationValue = 2;
      }
      else if (orientationTokenValue.equals(":west"))
      {
        orientationValue = 3;
      }
      while (world.getFacing() != orientationValue)
      {
        world.turnRight();
      }
      Robot.salida = "Command: face";
    }

    if (list != null) {
      list.add(face.image);
      list.add(orientation.image);
      }
  }
}

void put(boolean runWorld, List<String> list):
{
  Token itemToken;
  int numberItem;
  Token put;
}
{
 put = < PUT >(itemToken =  < ITEM >) numberItem=value()
  {
    if (numberItem != -1 && runWorld)
    {
      if (itemToken.image.equals(":balloons"))
      {
        world.putBalloons(numberItem);
      }
      else if (itemToken.image.equals(":chips"))
      {
        world.putChips(numberItem);
      }
      Robot.salida = "Command: put";
    }

    if (list != null) {
      list.add(put.image);
      list.add(itemToken.image);
      list.add(Integer.toString(numberItem));
      }
  }
}

void pick(boolean runWorld, List<String> list):
{
  Token itemToken;
  int numberItem;
  Token pick;
}
{
  pick = < PICK >(itemToken = < ITEM >) numberItem=value()
  {
    if (numberItem != -1 && runWorld)
    {
      if (itemToken.image.equals(":balloons"))
      {
        world.grabBalloons(numberItem);
      }
      else if (itemToken.image.equals(":chips"))
      {
        world.pickChips(numberItem);
      }
      Robot.salida = "Command: pick";
    }
    if (list != null) {
      list.add(pick.image);
      list.add(itemToken.image);
      list.add(Integer.toString(numberItem));
      }
  }
}

void moveDir(boolean runWorld, List<String> list):
{
  Token itemToken;
  int initialOrientation;
  int numberItem;
  Token moveDir;
  }
{
 moveDir = < MOVEDIR > numberItem=value()(itemToken = direction())
  {
    if (numberItem != -1 && runWorld)
    {
      //guardar orientacion inicial
      initialOrientation = world.getFacing();
      if (itemToken.image.equals(":right"))
      {
        world.turnRight();
        world.moveForward(numberItem, false);
      }
      else if (itemToken.image.equals(":left"))
      {
        world.turnRight();
        world.turnRight();
        world.turnRight();
        world.moveForward(numberItem, false);
	  }
	  else if (itemToken.image.equals(":front"))
	  {
	    world.moveForward(numberItem, false);
	  }
	  else if (itemToken.image.equals(":back"))
	  {
	    world.turnRight();
	    world.turnRight();
	    world.moveForward(numberItem, false);
	  }//Volver a orientacion de inicio
	  while (world.getFacing() != initialOrientation)
	  {
	    world.turnRight();
	  }
	  Robot.salida = "Command: move-dir";
    }
    if (list != null) {
      list.add(moveDir.image);
      list.add(Integer.toString(numberItem));
      list.add(itemToken.image);
      }
  }
}

void runDirs(boolean runWorld, List<String> list):
{
  List<String> directionsList = new ArrayList<>();
  Token directionReturn;
  int initialOrientation;
  Token runDirs;
}
{
 runDirs = < RUNDIRS >(directionReturn = direction() {
    directionsList.add(directionReturn.image);
    })+
  {
    if (runWorld)
    {
      for (String direction : directionsList)
      {
        initialOrientation = world.getFacing();
        
        if (direction.equals(":right"))
        {
          world.turnRight();
          world.moveForward(1, false);
        }
        else if (direction.equals(":left"))
        {
          world.turnRight();
          world.turnRight();
          world.turnRight();
          world.moveForward(1, false);
        }
        else if (direction.equals(":front"))
        {
          world.moveForward(1, false);
        }
        else if (direction.equals(":back"))
        {
          world.turnRight();
          world.turnRight();
          world.moveForward(1, false);
        }
		//Volver a orientacion de inicio
        while (world.getFacing() != initialOrientation)
        {
          world.turnRight();
        }
      }
      Robot.salida = "Command: rund dirs";
    }

    if (list != null) {
      list.add(runDirs.image);
      for  (String direction : directionsList) {
        list.add(direction);
        }
      }
  }
}

void moveFace(boolean runWorld, List<String> list):
{
  Token orientationToken;
  int moveValue;
  String orientationTokenValue;
  int orientationValue = -1;
  Token moveFace;
}
{
  moveFace = < MOVEFACE >moveValue = value()(orientationToken = < ORIENTATION >)
  {
    if (moveValue != -1 && runWorld)
    {
      orientationTokenValue = orientationToken.image;
      if(orientationTokenValue.equals(":north"))
      {
        orientationValue = 0;
      }
      else if (orientationTokenValue.equals(":south"))
      {
        orientationValue = 1;
      }
      else if (orientationTokenValue.equals(":east"))
      {
        orientationValue = 2;
      }
      else if (orientationTokenValue.equals(":west"))
      {
        orientationValue = 3;
      }
      while (world.getFacing() != orientationValue)
      {
        world.turnRight();
      }
      world.moveForward(moveValue, false);
      Robot.salida = "Command: move-face";
    }
    if (list != null) {
      list.add(moveFace.image);
      list.add(Integer.toString(moveValue));
      list.add(orientationToken.image);
      }
  }
}

void nullCommand():
{}
{
  < NULL >
  {
    Robot.salida = "Command: null";
  }
}

/*Control Structures*/
void controlStructure(boolean runWorld) :
{}
{
  (conditional(runWorld)|repeat(runWorld)|repeatTimes(runWorld))
}

void repeat(boolean runWorld) :
{}
{
  {
    Robot.salida = "Command: repeat";
  }
  <LOOP>condition()(parseCommand(runWorld, null)|block(runWorld))
}

void repeatTimes(boolean runWorld):
{
  int value;
  List<String> list = new ArrayList<>();
  int contador = 0;
  String comandoActual;
}
{
  {
    Robot.salida = "Command: repeatTimes";
  }
  <REPEAT>value=value()(<LPAREN>parseCommand(false, list)<RPAREN>)

  {
    while (contador < value) {
      comandoActual = list.get(0);
      if (comandoActual.equals("move")) {
        moveMethod(list.get(1));
        } else if (comandoActual.equals("=")) {
        assignMethod(list.get(1), list.get(2));
        } else if (comandoActual.equals("skip")) {
        skipMethod(list.get(1));    
        } else if (comandoActual.equals("turn")) {
		turnMethod(list.get(1));
        } else if (comandoActual.equals("face")) {
        faceMethod(list.get(1)); 
        } else if (comandoActual.equals("put")) {
		putMethod(list.get(1), list.get(2));
        } else if (comandoActual.equals("pick")) {
		pickMethod(list.get(1), list.get(2));
        } else if (comandoActual.equals("move-dir")) {
		moveDirMethod(list.get(1), list.get(2));
        } else if (comandoActual.equals("run-dirs")) {
		runDirsMethod(list);
        } else if (comandoActual.equals("move-face")) {
		moveFaceMethod(list.get(1), list.get(2));
        } else {
        System.out.println("._.");
          }
          contador +=1;
      }
  }
}

/*Conditionals*/
void conditional(boolean runWorld) :
{
  boolean conditionReturn;
}
{
  <IF>(conditionReturn = condition())
  {
    if (runWorld)
    {
      Robot.salida = "Command: if";
      if (conditionReturn)
      {
        block(true);
        block(false);
      }
      else
      {
        block(false);
        block(true);
      }
    }
    else
    {
      block(false);
      block(false);
    }
  }
}

boolean condition() :
{
  boolean valueReturn;
  }
{
  <LPAREN>(valueReturn = facingCondition()
  |valueReturn = blockedCondition()
  |valueReturn = canputCondition()
  |valueReturn = canpickCondition()
  |valueReturn = canmoveCondition()
  |valueReturn = iszeroCondition()
  |valueReturn = notCondition())
  <RPAREN>
  {
    System.out.println(valueReturn);
    return valueReturn;
  }
}

boolean facingCondition() :
{
  Token orientationToken;
  String orientationTokenValue;
  int orientationValue = -1;
  boolean returnValue;
}
{
  <FACING>(orientationToken = <ORIENTATION>)
  {
    orientationTokenValue = orientationToken.image;
    if(orientationTokenValue.equals(":north")) {
      orientationValue = 0;
      } else if (orientationTokenValue.equals(":south")) {
        orientationValue = 1;
      } else if (orientationTokenValue.equals(":east")) {
        orientationValue = 2;
      } else if (orientationTokenValue.equals(":west")) {
		orientationValue = 3;
        }
     if (orientationValue == world.getFacing()) {
       returnValue = true;
        } else {
        returnValue = false;
          }
     return returnValue;
  }
}

boolean blockedCondition() :
{
  Point position;
  int orientationValue;
  boolean returnValue = false;
}
{
  <BLOCKED>
  {
    orientationValue = world.getFacing();
    position = (Point) world.getPosition().clone();
    if (orientationValue == 0) {
      position.y -= 1;
     } else if (orientationValue == 1) {
       position.y +=1;
     } else if (orientationValue == 2) {
       position.x +=1;
     } else if (orientationValue == 3) {
       position.x -=1;
     }
     returnValue = world.isBlocked(position);
     return returnValue;
  } 
}

boolean canputCondition() :
{
  boolean returnValue = true;
  Token item;
  int itemValue = 0;
}
{
  <CANPUT>(item = <ITEM>)(itemValue = value())
  {
	if (itemValue != -1) {
	  if (item.image.equals(":chips")) {
	 	 if (itemValue > world.freeSpacesForChips() || itemValue < 0 || world.getMyChips() < itemValue) {
	  	  returnValue = false;
	   	 }
	  } else if(item.image.equals(":balloons")) {
	    if (itemValue < 0 || world.getMyBalloons() < itemValue) {
	      returnValue = false;
	      }
	    }	    
	  } else {
	    returnValue = false;
	    }
	 return returnValue;
  }
}

boolean canpickCondition():
{
  Token item;
  int value;
}
{
  <CANPICK>item=<ITEM>value=value()
  {
    if (value == -1) { return false; }
    
    if (item.image.equals(":chips"))
    {
      if (value > 0 && world.chipsToPick() >= value)
      {
        return true;
      }
      else
      {
        return false;
      }
    }

    if (item.image.equals(":balloons"))
    {
      if (value > 0 && world.countBalloons() >= value)
      {
        return true;
      }
      else
      {
        return false;
      }
    }
  }
}
//(if (can-move? :south)(move-face 3 :south)(null))
boolean canmoveCondition() :
{
  boolean outOfBounds = false;
  boolean canMove = false;
  Token tokenOrientation;
  String orientationTokenValue;
  Point currentPosition = (Point) world.getPosition().clone();
}
{
 <CANMOVE>(tokenOrientation = <ORIENTATION>)
 
 {
   orientationTokenValue = tokenOrientation.image;
   if(orientationTokenValue.equals(":north")) {
      currentPosition.y -= 1;
      
      //comprobar si está dentro del tablero
    if (currentPosition.y > world.getN() || currentPosition.y < 1) {
      outOfBounds = true;
      }
        
      } else if (orientationTokenValue.equals(":south")) {
        currentPosition.y += 1;

        //comprobar si está dentro del tablero
    	if (currentPosition.y > world.getN() || currentPosition.y < 1) {
      		outOfBounds = true;
      		}
        
      } else if (orientationTokenValue.equals(":east")) {
        currentPosition.x += 1;
        
        if (currentPosition.x > world.getN() || currentPosition.x < 1) {
      		outOfBounds = true;
      	}
        
      } else if (orientationTokenValue.equals(":west")) {
		currentPosition.x -= 1;

		//comprobar si está dentro del tablero
		if (currentPosition.x > world.getN() || currentPosition.x < 1) {
      		outOfBounds = true;
      	}
        }
        
    //comprobar isBlocked
    if (!world.isBlocked(currentPosition) && !outOfBounds) {
      canMove = true;
      }
        
   return canMove;
 } 
}

boolean iszeroCondition() :
{
  boolean returnBoolValue = false;
  int returnNumberValue;
}
{
  <ISZERO>(returnNumberValue = value())
  {
    if (returnNumberValue == 0) {
      returnBoolValue = true;
      }
    return returnBoolValue;
  }
}

boolean notCondition() :
{
  boolean returnValue = false;
  boolean conditionValue = false;
}
{
  <NOT>(conditionValue = condition())
  {
    if (!conditionValue) {
      returnValue = true;
      }
    return returnValue;
  }
}

/* Auxiliary Functions*/
int varCall():
{
	int value = -1;
}
{
  <WORD>
  {
    if (Robot.variableMap.containsKey(token.image))
    {
      try
      {
        value = Robot.variableMap.get(token.image);
      }
      catch (NumberFormatException ee)
      {
        throw new Error("Hay overflow con el valor: "+value);
      }

      return value;
    }
    if (!Robot.variableMap.containsKey(token.image)) {
		if (!Robot.paramFunctionMap.containsKey(token.image)) {
			throw new ParseException("Error: La variable " + token.image + " no está definida.");
        }
    }
    
    return value; //Si se retorna -1 significa que el varCall() corresponde al parámetros de una función
  }
}

int value() throws Error:
{int value = -1;}
{
  (<NUM>
  {
    try
    {
      value = Integer.parseInt(token.image);
    }
   	catch (NumberFormatException ee) 
		{
			throw new Error("Hay overflow con el valor: "+token.image);
		}
	return value;
  }
  |<CONSTANT>
  {
    String image = token.image;
  	if (image.equals("dim")) { value = world.getN(); }
  	else if (image.equals("myxpos")) { value = (int) world.getPosition().getX(); }
  	else if (image.equals("myypos")) { value = (int) world.getPosition().getY(); }
  	else if (image.equals("mychips")) { value = world.getMyChips(); }
  	else if (image.equals("myballons")) { value = world.getMyBalloons(); }
  	else if (image.equals("ballonshere")) { value = world.countBalloons(); }
	else if (image.equals("chipshere")) { value = world.chipsToPick(); }
  	else if (image.equals("spaces")) { value = world.freeSpacesForChips(); }
  
	return value;
  }
  |value = varCall()
  {	return value; })//Si se retorna -1 significa que el varCall() corresponde al parámetros de una función
}

//value() retorna el valor numérico para usarlo con los métodos de world

Token direction():
{
  Token directionToken;
}
{
  directionToken = < DIRECTION >
  {
    if (!(token.image.equals(":front") || token.image.equals(":right") || token.image.equals(":left") || token.image.equals(":back"))) {
		throw new ParseException("Error: la dirección " + token.image + " no es válida con el comando move-dir o run-dirs.");
    }
    return directionToken;
  }
}

//Initial Rule
boolean command(Console sistema):
{}
{
	(<LPAREN>(varDefinition()|procedureDefinition()|instruction(true)) 
	{
	    try {
    			 Thread.sleep(900);
    	    } catch (InterruptedException e) {
    			        System.err.format("IOException: %s%n", e);
    		    }

		sistema.printOutput(salida);
		return true;
	})+

	| <EOF> {return false;} 
}
